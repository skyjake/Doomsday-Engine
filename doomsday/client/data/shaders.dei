# Doomsday's Core Set of Shaders
#
# In each "shader" block, there can be:
# - path:          path to both the .vsh and .fsh files (omit extension:
#                  "shaders/test" => shaders/test.vsh, shaders/test.fsh)
# - path.vertex:   path to the vertex shader file
# - path.fragment: path to the fragment shader file
# - vertex:        source of the vertex shader
# - fragment:      source of the fragment shader

group generic {
    # Simple shader with untextured vertices. There is an additional constant
    # color applied to all vertices. Uses a combined model-view-projection
    # matrix.
    shader color_ucolor {
        vertex = "
            uniform highp mat4 uMvpMatrix;
            uniform highp vec4 uColor;
            attribute highp vec4 aVertex;
            attribute highp vec4 aColor;
            varying highp vec4 vColor;
            
            void main(void) {
                gl_Position = uMvpMatrix * aVertex;
                vColor = uColor * aColor;
            }"
        fragment = "
            varying highp vec4 vColor;
            
            void main(void) {
                gl_FragColor = vColor;
            }"
    }
    
    group textured {    
        # Simple shader with one texture plus a color per vertex. Uses a
        # combined model-view-projection matrix.
        shader color {
            vertex = "
                uniform highp mat4 uMvpMatrix;
                attribute highp vec4 aVertex;
                attribute highp vec2 aUV;
                attribute highp vec4 aColor;
                varying highp vec2 vUV;
                varying highp vec4 vColor;
            
                void main(void) {
                    gl_Position = uMvpMatrix * aVertex;
                    vUV = aUV;
                    vColor = aColor;
                }"
            fragment = "
                uniform sampler2D uTex;
                varying highp vec2 vUV;
                varying highp vec4 vColor;

                void main(void) {
                    gl_FragColor = vColor * texture2D(uTex, vUV);
                }"
        }
    
        # Simple shader with one texture plus a color per vertex. There is
        # an additional constant color applied to all vertices. Uses a
        # combined model-view-projection matrix.
        shader color_ucolor {
            vertex = "
                uniform highp mat4 uMvpMatrix;
                uniform highp vec4 uColor;
                attribute highp vec4 aVertex;
                attribute highp vec2 aUV;
                attribute highp vec4 aColor;
                varying highp vec2 vUV;
                varying highp vec4 vColor;
            
                void main(void) {
                    gl_Position = uMvpMatrix * aVertex;
                    vUV = aUV;
                    vColor = aColor * uColor;
                }"
            fragment = "
                uniform sampler2D uTex;
                varying highp vec2 vUV;
                varying highp vec4 vColor;

                void main(void) {
                    gl_FragColor = vColor * texture2D(uTex, vUV);
                }"
        }
    }
}

group fx {
    group blur {
        shader horizontal {
            path.vertex = "shaders/blur.vsh"
            fragment = "
                uniform sampler2D uTex;
                uniform highp vec2 uBlurStep;
                varying highp vec2 vUV;
                varying highp vec4 vColor;
                void main(void) {
                    highp vec4 sum = vec4(0.0);
                    sum += texture2D(uTex, vec2(vUV.s - 4.0 * uBlurStep.s, vUV.t)) * 0.05;
                    sum += texture2D(uTex, vec2(vUV.s - 3.0 * uBlurStep.s, vUV.t)) * 0.09;
                    sum += texture2D(uTex, vec2(vUV.s - 2.0 * uBlurStep.s, vUV.t)) * 0.123;
                    sum += texture2D(uTex, vec2(vUV.s - uBlurStep.s,       vUV.t)) * 0.154;
                    sum += texture2D(uTex, vUV)                                    * 0.165;
                    sum += texture2D(uTex, vec2(vUV.s + uBlurStep.s,       vUV.t)) * 0.154;
                    sum += texture2D(uTex, vec2(vUV.s + 2.0 * uBlurStep.s, vUV.t)) * 0.123;
                    sum += texture2D(uTex, vec2(vUV.s + 3.0 * uBlurStep.s, vUV.t)) * 0.09;
                    sum += texture2D(uTex, vec2(vUV.s + 4.0 * uBlurStep.s, vUV.t)) * 0.05;
                    gl_FragColor = sum;
                    gl_FragColor.a = 1.0;
                }"
        }
        shader vertical {
            path.vertex = "shaders/blur.vsh"
            fragment = "
                uniform sampler2D uTex;
                uniform highp vec2 uBlurStep;
                varying highp vec2 vUV;
                varying highp vec4 vColor;
                void main(void) {
                    highp vec4 sum = vec4(0.0);
                    sum += texture2D(uTex, vec2(vUV.s, vUV.t - 4.0 * uBlurStep.t)) * 0.05;
                    sum += texture2D(uTex, vec2(vUV.s, vUV.t - 3.0 * uBlurStep.t)) * 0.09;
                    sum += texture2D(uTex, vec2(vUV.s, vUV.t - 2.0 * uBlurStep.t)) * 0.123;
                    sum += texture2D(uTex, vec2(vUV.s, vUV.t - uBlurStep.t      )) * 0.154;
                    sum += texture2D(uTex, vUV)                                    * 0.165;
                    sum += texture2D(uTex, vec2(vUV.s, vUV.t + uBlurStep.t      )) * 0.154;
                    sum += texture2D(uTex, vec2(vUV.s, vUV.t + 2.0 * uBlurStep.t)) * 0.123;
                    sum += texture2D(uTex, vec2(vUV.s, vUV.t + 3.0 * uBlurStep.t)) * 0.09;
                    sum += texture2D(uTex, vec2(vUV.s, vUV.t + 4.0 * uBlurStep.t)) * 0.05;
                    gl_FragColor = sum * vColor;
                }"
        }
    }
}

group vr {
    group oculusrift {
        shader barrel {
            vertex = "
                #version 120
                attribute highp vec4 aVertex;
                attribute highp vec2 aUV;
                varying highp vec2 vTexCoord;
                void main() {
                    gl_Position = aVertex;
                    vTexCoord = aUV;
                }"
            path.fragment = "shaders/oculusrift-barrel.fsh"
        }
    }
}

group debug {
    group textured {    
        # Visualize alpha information.
        shader alpha {
            vertex = "
                uniform highp mat4 uMvpMatrix;
                attribute highp vec4 aVertex;
                attribute highp vec2 aUV;
                attribute highp vec4 aColor;
                varying highp vec2 vUV;
                varying highp vec4 vColor;
            
                void main(void) {
                    gl_Position = uMvpMatrix * aVertex;
                    vUV = aUV;
                    vColor = aColor;
                }"
            fragment = "
                uniform sampler2D uTex;
                varying highp vec2 vUV;
                varying highp vec4 vColor;

                void main(void) {
                    highp vec4 col = vColor * texture2D(uTex, vUV);
                    gl_FragColor = vec4(col.a, col.a, col.a, 1.0);
                }"
        }
    }
}
