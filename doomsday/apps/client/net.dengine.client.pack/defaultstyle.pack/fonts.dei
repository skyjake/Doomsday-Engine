# Fonts for the default UI style
#
# - family: font family name
# - size can be "pt" or "px", defaults to "pt"
# - weight: normal, bold, light
# - style: normal, italic
# - transform: normal, uppercase, lowercase

script {
    import Version, gui, DisplayMode

    # Load the fonts contained in this pack.
    def loadFonts()
        import App
        fontDir = __file__.fileNamePath() / "fonts"
        fontStyles = ['Regular', 'Bold', 'Italic', 'BoldItalic']
        # Light fonts are a bit too thin for low-res displays.
        if DisplayMode.PIXEL_RATIO > 1
            fontStyles += ['Light', 'LightItalic']
        end
        for style in fontStyles
            App.loadFont(fontDir / ("SourceSansPro-%s.ttf" % style))
        end
    end
}

group {
    condition: Version.OS == 'windows'

    script { loadFonts() }

    font default {
        family: Source Sans Pro
        size: 12pt
        weight: normal
        style: normal
    }

    font monospace inherits default {
        family: Courier New
        size: 10pt
    }
}

group {
    condition: Version.OS == 'macx' or Version.OS == 'ios'

    script {
        # Define mappings for native font styles and weights.
        import App
        if Version.OS == 'macx'
            prefix = '.SFNS'
        else
            prefix = '.SFUI'
        end
        App.addFontMapping("SF UI Text", {
            ['regular', 25]: prefix + 'Text-Light',
            ['regular', 50]: prefix + 'Text-Regular',
            ['regular', 75]: prefix + 'Text-Bold',
            ['italic',  25]: prefix + 'Text-LightItalic',
            ['italic',  50]: prefix + 'Text-Italic',
            ['italic',  75]: prefix + 'Text-BoldItalic'
        })
        App.addFontMapping("SF UI Display", {
            ['regular', 25]: prefix + 'Display-Light',
            ['regular', 50]: prefix + 'Display-Regular',
            ['regular', 75]: prefix + 'Display-Bold',
            ['italic',  25]: prefix + 'Text-LightItalic',    # Only non-italic available.
            ['italic',  50]: prefix + 'Text-Italic',
            ['italic',  75]: prefix + 'Text-BoldItalic'
        })
        App.addFontMapping("Menlo", {
            ['regular', 25]: 'Menlo-Regular',
            ['regular', 50]: 'Menlo-Regular',
            ['regular', 75]: 'Menlo-Bold',
            ['italic',  25]: 'Menlo-Italic',
            ['italic',  50]: 'Menlo-Italic',
            ['italic',  75]: 'Menlo-BoldItalic'
        })
    }

    font default {
        family: SF UI Text
        size $: gui.scale('16pt', DisplayMode.PIXEL_RATIO)
        weight: normal
        style: normal
    }

    font monospace inherits default {
        family: Menlo
        size $: gui.scale('12pt', DisplayMode.PIXEL_RATIO)
    }
}

group {
    condition: Version.OS == 'unix'

    font default {
        family: Liberation Sans
        size $: gui.scale("13pt", DisplayMode.PIXEL_RATIO)
        weight: normal
        style: normal
    }

    font monospace inherits default {
        family: FreeMono
        size $: gui.scale("9pt", DisplayMode.PIXEL_RATIO)
    }
}

font title inherits default {
    size $: gui.scale(self.size, 1.75)
    weight: light
}

font heading inherits title {
    size $: gui.scale(default.size, 1.2)
}

script {
    if Version.OS == 'macx' or Version.OS == 'ios'
        # Use the larger variant.
        title.family   = "SF UI Display"
        heading.family = "SF UI Display"
    end
}

font small inherits default {
    size $: gui.scale(self.size, 0.75)
}

menu {
    font empty inherits heading {
        weight: bold
    }
}

editor {
    font plaintext inherits default {}
    font hint.default inherits default {
        style: italic
        weight: light
    }
    font hint.small inherits small {
        style: italic
        weight: light
    }
}

separator {
    font empty inherits default {
        size $: gui.scale(self.size, 0.5)
    }
    font label inherits default {
        #condition: Version.OS == 'macx' or Version.OS == 'ios'
        size $: gui.scale(self.size, 0.7)
        transform: uppercase
    }
    #font label inherits small {
    #    condition: Version.OS != 'macx' and Version.OS != 'ios'
    #    weight: bold
    #}
    font annotation inherits small {}
}

choice {
    font selected inherits default {
        weight: bold
    }
}

tab {
    font label inherits title {
        size $: gui.scale(self.size, 0.75)
    }
    font selected inherits tab.label {}
}

slider {
    font label inherits small {}
    font value inherits slider.label {
        weight: bold
    }
}

log {
    font normal inherits default {}
}

document {
    font monospace inherits monospace {}
}

script {
    # Make monospace a bit smaller on Windows - Courier New is pretty wide.
    if Version.OS == 'windows'
        document.monospace.size = '9pt'
    end
}
