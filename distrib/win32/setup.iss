; Script generated by the Inno Setup Script Wizard.

[Setup]
OutputBaseFilename=deng-1.9.0-beta5-setup
VersionInfoVersion=1.9.0.5
AppName=Doomsday Engine
AppVerName=Doomsday Engine 1.9.0-beta5
AppPublisher=deng Team
AppPublisherURL=http://sourceforge.net/projects/deng/
AppSupportURL=http://dew.dengine.net/
AppUpdatesURL=http://sourceforge.net/projects/deng/
DefaultDirName={pf}\Doomsday
DefaultGroupName=Doomsday Engine
AllowNoIcons=yes
SetupIconFile=..\..\..\snowberry\graphics\snowberry.ico
SourceDir=..\..\doomsday\build\win32
OutputDir=..\..\..\distrib\win32\packages
Compression=lzma/max
;Compression=zip/1
SolidCompression=yes

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Dirs]
Name: "{app}\bin"
Name: "{app}\data"
Name: "{app}\data\jdoom"
Name: "{app}\data\jheretic"
Name: "{app}\data\jhexen"
Name: "{app}\data\jdoom\auto"
Name: "{app}\data\jheretic\auto"
Name: "{app}\data\jhexen\auto"
Name: "{app}\defs"
Name: "{app}\defs\jdoom"
Name: "{app}\defs\jheretic"
Name: "{app}\defs\jhexen"
Name: "{app}\defs\jdoom\auto"
Name: "{app}\defs\jheretic\auto"
Name: "{app}\defs\jhexen\auto"
Name: "{app}\doc"
Name: "{app}\snowberry"
Name: "{app}\snowberry\profiles"
Name: "{app}\snowberry\addons"
Name: "{app}\snowberry\graphics"
Name: "{app}\snowberry\conf"
Name: "{app}\snowberry\lang"
Name: "{app}\snowberry\plugins"

[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
; Binaries
Source: "Bin\Release\Doomsday.exe"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "Bin\Release\jDoom.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "Bin\Release\jHeretic.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "Bin\Release\jHexen.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "Bin\Release\dr*.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "Bin\Release\dsCompat.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "Bin\Release\dp*.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
; Libraries
Source: "SDL.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "SDL_net.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "eax.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\..\external\lzss\win32\lzss.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\..\external\zlib\win32\zlib.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
; Snowberry
Source: "..\..\..\snowberry\dist\*"; DestDir: "{app}\snowberry"; Flags: ignoreversion
Source: "..\..\..\snowberry\graphics\*.bmp"; DestDir: "{app}\snowberry\graphics"
Source: "..\..\..\snowberry\graphics\*.png"; DestDir: "{app}\snowberry\graphics"
Source: "..\..\..\snowberry\graphics\*.jpg"; DestDir: "{app}\snowberry\graphics"
Source: "..\..\..\snowberry\graphics\*.ico"; DestDir: "{app}\snowberry\graphics"
Source: "..\..\..\snowberry\lang\*.lang"; DestDir: "{app}\snowberry\lang"
Source: "..\..\..\snowberry\plugins\*.py"; DestDir: "{app}\snowberry\plugins"; Excludes: "observer.py"
Source: "..\..\..\snowberry\plugins\tab30.plugin\Contents\tab30\*.py"; DestDir: "{app}\snowberry\plugins\tab30.plugin\Contents\tab30"
Source: "..\..\..\snowberry\profiles\*.prof"; DestDir: "{app}\snowberry\profiles"
Source: "..\..\..\snowberry\conf\*.conf"; DestDir: "{app}\snowberry\conf"
; Documentation
;Source: "Doc\jDoom\Doomlic.txt"; DestDir: "{app}\doc\jdoom"
;Source: "Doc\jDoom\jDoom.txt"; DestDir: "{app}\doc\jdoom"
;Source: "Doc\Ravenlic.txt"; DestDir: "{app}\doc"
; Resources
Source: "doomsday.pk3"; DestDir: "{app}\data"
Source: "jdoom.pk3"; DestDir: "{app}\data\jdoom"
Source: "jheretic.pk3"; DestDir: "{app}\data\jheretic"
Source: "jhexen.pk3"; DestDir: "{app}\data\jhexen"

[INI]
Filename: "{app}\doomsday.url"; Section: "InternetShortcut"; Key: "URL"; String: "http://sourceforge.net/projects/deng/"

[Icons]
Name: "{group}\Doomsday Engine"; Filename: "{app}\snowberry\snowberry.exe"; WorkingDir: "{app}\snowberry"
Name: "{group}\{cm:ProgramOnTheWeb,Doomsday Engine}"; Filename: "{app}\doomsday.url"
Name: "{group}\{cm:UninstallProgram,Doomsday Engine}"; Filename: "{uninstallexe}"
Name: "{userdesktop}\Doomsday Engine"; Filename: "{app}\snowberry\snowberry.exe"; WorkingDir: "{app}\snowberry"; Tasks: desktopicon

[Run]
Filename: "{app}\snowberry\snowberry.exe"; Description: "{cm:LaunchProgram,Doomsday Engine}"; WorkingDir: "{app}\snowberry"; Flags: nowait postinstall skipifsilent

[UninstallDelete]
Type: files; Name: "{app}\doomsday.url"
Type: files; Name: "{app}\snowberry\plugins\*.pyc"
Type: files; Name: "{app}\snowberry\*.log"
Type: dirifempty; Name: "{app}\snowberry\plugins"
Type: dirifempty; Name: "{app}\snowberry"
Type: dirifempty; Name: "{app}"

